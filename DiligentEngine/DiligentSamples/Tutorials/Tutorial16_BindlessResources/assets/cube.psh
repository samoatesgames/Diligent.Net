#ifdef BINDLESS

#ifndef NUM_TEXTURES
#   define NUM_TEXTURES 1
#endif

#ifdef VULKAN
// NonUniformResourceIndex is not supported by GLSLang
#   define NonUniformResourceIndex(x) x
#endif

Texture2D     g_Texture[NUM_TEXTURES];
SamplerState  g_Texture_sampler; // By convention, texture samplers must use the '_sampler' suffix

#else

Texture2D     g_Texture;
SamplerState  g_Texture_sampler; // By convention, texture samplers must use the '_sampler' suffix

#endif

struct PSInput 
{ 
    float4 Pos      : SV_POSITION; 
    float2 UV       : TEX_COORD; 
    uint   TexIndex : TEX_ARRAY_INDEX;
};

struct PSOutput
{
    float4 Color : SV_TARGET;
};

void main(in  PSInput  PSIn,
          out PSOutput PSOut)
{
    float4 Color;
#ifdef BINDLESS
    Color = g_Texture[NonUniformResourceIndex(PSIn.TexIndex)].Sample(g_Texture_sampler, PSIn.UV);
#else
    Color = g_Texture.Sample(g_Texture_sampler, PSIn.UV);
#endif
#if CONVERT_PS_OUTPUT_TO_GAMMA
    // Use fast approximation for gamma correction.
    Color.rgb = pow(Color.rgb, float3(1.0 / 2.2, 1.0 / 2.2, 1.0 / 2.2));
#endif
    PSOut.Color = Color;
}
